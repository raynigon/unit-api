buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'signing'
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    // Auto Update Versions
    id 'se.patrikerdes.use-latest-versions' version '0.2.15'
    id 'com.github.ben-manes.versions' version '0.38.0'

    // Auto Release
    id('net.researchgate.release') version '2.8.1'
}

allprojects {
    group = 'com.raynigon.unit-api'
    // version = project.version
    sourceCompatibility = '1.8'
    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'signing'
    apply plugin: 'net.researchgate.release'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    apply from: "$rootDir/gradle/scripts/publishing.gradle"
    apply from: "$rootDir/gradle/scripts/versionClass.gradle"
    apply from: "$rootDir/gradle/scripts/release.gradle"

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    sourceSets {
        main {
            java.srcDirs += "build/generated/sources/version/java"
        }
    }

    dependencies {
        implementation 'javax.measure:unit-api:2.1.2'
        implementation 'org.apache.commons:commons-lang3:3.11'

        testImplementation 'org.codehaus.groovy:groovy-all:2.4.20'
        testImplementation 'org.spockframework:spock-core:1.2-groovy-2.4'
        testRuntime "net.bytebuddy:byte-buddy:1.10.21"
    }
}

bootJar {
    enabled = false
}

apply from: "$rootDir/gradle/scripts/updateMkdocsConfig.gradle"
tasks.getByName("afterReleaseBuild").dependsOn(
        tasks.getByName("updateMkdocsConfig")
)
