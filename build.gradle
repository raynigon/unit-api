buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    // Auto Update Versions
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'com.github.ben-manes.versions' version '0.52.0'

    id 'maven-publish'
    id "net.researchgate.release" version "3.1.0"
}

evaluationDependsOnChildren()

allprojects {
    group = 'com.raynigon.unit-api'
}

release {
    preTagCommitMessage = "[Release] Version:"
    tagCommitMessage = "[Release] Version:"
    newVersionCommitMessage = "[Release] Update Version:"
    tagTemplate = 'v${version}'
    buildTasks = ['publish']

    git {
        requireBranch.set("release")
    }
}

def updateMkdocsConfig = tasks.register("updateMkdocsConfig") {
    doFirst {
        def cfg = project.file(project.file('mkdocs.yml'))
        cfg.text = cfg.text.replaceAll(/(?m)latest_version:.*/, "latest_version: ${project.version}")
    }
}

tasks.named("afterReleaseBuild").configure {
    dependsOn(
            subprojects.collect { it.tasks.named("publish") },
            updateMkdocsConfig
    )
}
